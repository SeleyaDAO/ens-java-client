/*
Copyright 2022 Seleya DAO

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

https://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

plugins {
    id 'org.springframework.boot' version '2.7.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java-library'
    /* artifact publication plugins */
	id 'maven-publish'
	id 'signing'
	id('io.github.gradle-nexus.publish-plugin') version '1.1.0'
}

group = 'xyz.seleya.product'
version = '0.0.8'
sourceCompatibility = '11'

repositories {
    mavenCentral()

    maven { // for java-cid
        url "https://jitpack.io"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.web3j:core:4.8.7'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.3'
    implementation group: 'com.github.ipld', name: 'java-cid', version: '1.3.1'
    implementation group: 'com.github.ipfs', name: 'java-ipfs-http-client', version: '1.3.3'
    implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.3'
    testImplementation group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '4.9.3'
}

tasks.named('test') {
    useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

java {
	withJavadocJar()
	withSourcesJar()
}

/* Publishing artifacts to Nexus Maven Repository - start */
publishing {
    publications {
		maven(MavenPublication) {
			from components.java

			groupId = "${project.group}"
			artifactId = "${project.name}"
			version = "${project.version}"

			pom {
				name = 'ens-client'
				description = '''A simple and naive java wrapper library for retrieving 
ENS-related information from an Ethereum node. This library is built on
top of the excellent web3j project (https://github.com/web3j/web3j).'''
				url = 'https://github.com/SeleyaDAO/ens-java-client'
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				developers {
					developer {
						id = 'trackball3r'
						name = 'trackball3r'
					}
					developer {
						id = 'skimonk3y'
						name = 'skimonk3y'
					}
				}
				scm {
					url = 'https://github.com/SeleyaDAO/ens-java-client'
					connection = 'scm:git://github.com/SeleyaDAO/ens-java-client.git'
					developerConnection = 'scm:git://github.com/SeleyaDAO/ens-java-client.git'
				}
			}
		}
	}
}

nexusPublishing {
	repositories {
		sonatype {  //only for users registered in Sonatype after 24 Feb 2021
			nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
			snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
		}
	}
}

signing {
	sign publishing.publications.maven
}
/* Publishing artifacts to Nexus Maven Repository - end */

/* The gradle build also generates a version.txt that gets included in the jar - start */
ext.genOutputDir = file("$buildDir/generated-resources")

task generateVersionTxt()  {
	ext.outputFile = file("$genOutputDir/version.txt")
	outputs.file(outputFile)
	doLast {
		outputFile.text = """GroupId: ${project.group}
Name: ${project.name}
Version: $version
Build-time: ${java.time.LocalDateTime.now()}
"""
	}
}
sourceSets.main.output.dir genOutputDir, builtBy: generateVersionTxt
/* The gradle build also generates a version.txt that gets included in the jar - end */
